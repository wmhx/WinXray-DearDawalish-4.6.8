//ssr 内核进程（SSR）
import wsock.tcp.server;
import xray.core.ssrJson;
import process.popen;
import config;

namespace xray.core.ssr;

var prcsSsr;
import process.job.limitKill;
restart = function(editor,outbound,ssrSocksPort){
 
	if(prcsSsr){
		prcsSsr.terminate();
		prcsSsr = null;
	}

	var corePath = getPath(editor.hwnd);
	if(!corePath){ return false,"启动失败,未找到 ssr-client.exe"; } 

	import xray.core.ssrJson;
	var jsonPath,err = ..xray.core.ssrJson.write(
		..io.joinpath(..io.splitpath(corePath).dir,"config.json")
		,outbound,ssrSocksPort);

	if(!jsonPath){
		return false,err:"启动失败,写入 SSR 配置文件遇到错误！"; 
	}
	 
	  
	var err;
	prcsSsr,err = ..process.popen(corePath," -c=config.json" );
	if(!prcsSsr){
		..publish("uiCommand.print",err:"启动 SSR Core 时遇到未知错误!");  
		return;
	}
	else{
		..publish("uiCommand.print","SSR Core 已启动，端口:" + ssrSocksPort);  
	}
	prcsSsr.assignToJobObject(process.job.limitKill);
	prcsSsr.codepage = 65001;	
	prcsSsr.logResponse(editor); 
	 
	return true;
}

stop = function(){
	if(prcsSsr){
		prcsSsr.terminate();
		prcsSsr = null;
	} 
}

var getSsrCoreUrl = function(){
    var msgDlg = ..win.dlg.message(..mainForm); 
	var form = msgDlg.create('正在获取SSR Core（ShadowsocksR-native）最新版本',,true)
	form.icon = '\uF1D8';
	form.progress.startProgress(50); 
	
	var url,versionTag = ..win.invoke(function(){
    	import process;
		import curl;
		import web.json;
		
		var http = curl.easy();
		http.setHttpHeader("User-Agent: curl/aardio.curl.easy");
		var response = http.get("https://api.github.com/repos/ShadowsocksR-Live/shadowsocksr-native/releases");
		http.close();
		if(!response) return;
		var data = web.json.parse(response)
		for(k,v in data){
			// 第一个prerelease不为为false的就是最新的tag
			if (data[k]['prerelease'] == false) {
				tag = data[k]['tag_name']
				break
			}
		}		
		if(!tag) return;
		
		return "https://github.com/ShadowsocksR-Live/shadowsocksr-native/releases/download/" 
			+ tag + "/ssr-native-windows-" +	(..process().isWow64() ? "x64" : "x86") + ".zip",tag	
	});
	
	form.close();
	
	return url,versionTag; 
}

downloadCore = function(){
	..mainForm.disabled = true;

	var url,versionTag = getSsrCoreUrl();
	if(versionTag){
		..publish("uiCommand.print","SSR Core（ShadowsocksR-native）最新版本: " + versionTag);
	}
	
	var coreDir = getCoreDir();
	if(!url) {
		..publish("uiCommand.print","请下载 SSR Core（ShadowsocksR-native）解压到以下目录内:");
		..publish("uiCommand.print",coreDir);
		
		..mainForm.disabled = false;
		self.lastDownloadingCoreFailed = true;
		return null;
	}
	
	// 全局代理的情况下，不使用加速节点
	if(..config.proxy.mode != "proxy"){
		var msgDlg = ..win.dlg.message(..mainForm); 
		var form = msgDlg.create('正在查询 Github 加速地址',,true);
		form.icon = '\uF1D8';
		form.progress.startProgress(50); 
	
		url = ..win.invoke(function(url){
			import xray.githubMirror;
			return xray.githubMirror.getMirrorUrl(url);
		},url);
		form.close();
		..publish("uiCommand.print","Github加速地址:");
		..publish("uiCommand.print",url);	
	}
	
	import zlib.httpFile;
	if( ..zlib.httpFile.download(url,"正在下载 SSR Core（ShadowsocksR-native）" 
		,..io.fullpath("/download/ssr")
		,coreDir,,..mainForm.hwnd) ){
			
		..mainForm.disabled = false;
		self.lastDownloadingCoreFailed = null;
		..config.core.ssrVersion = versionTag;
		return ..io.exist(..io.joinpath(coreDir,"ssr-client.exe"));
	}
	else {
		..mainForm.disabled = false;
		self.lastDownloadingCoreFailed = true;
		
		..publish("uiCommand.print","请到以下网址下载 SSR Core:");
		..publish("uiCommand.print",url);
		
		..publish("uiCommand.print","下载后请解压到以下目录内:");
		..publish("uiCommand.print",coreDir); 
	} 	
}
 
 
getPath = function(hwnd){
	var path = ..io.fullpath("/core/ssr/ssr-client.exe");
	if(..io.exist(path)){
		return path;
	}
		
	
	if(self.lastDownloadingCoreFailed){
		return;
	}
	

	return downloadCore();
} 

getCoreDir = function(hwnd){
	var path = ..io.fullpath("/core/ssr/ssr-client.exe");
	if(..io.exist(path)){
		return ..io.fullpath("/core/ssr/");
	}
		
	return ..io.fullpath("/core/ssr/");
} 

updateCore = function(){
  	..mainForm.disabled = true; 
  	
	var coreDir = getCoreDir(); 
	var url,versionTag = getSsrCoreUrl();
	if(!url){		
		..mainForm.disabled = false;
		self.lastDownloadingCoreFailed = true;
		return null;
	}
	
	// 如果已是最新版本，则不下载
	var path = ..io.fullpath("/core/ssr/ssr-client.exe");
	if(versionTag == ..config.core.ssrVersion && ..io.exist(path)){
		return versionTag,true;
	}
	
	// 全局代理的情况下，不使用加速节点
	if(..config.proxy.mode != "proxy"){
		var msgDlg = ..win.dlg.message(..mainForm); 
		var form = msgDlg.create('正在查询 Github 加速地址',,true);
		form.icon = '\uF1D8';
		form.progress.startProgress(50); 
	
		url = ..win.invoke(function(url){
			import xray.githubMirror;
			return xray.githubMirror.getMirrorUrl(url);
		},url);
		form.close();
		..publish("uiCommand.print","Github加速地址:");
		..publish("uiCommand.print",url);	
	}
	
	import fsys;
	fsys.delete(..io.fullpath("/temp/ssr/"))
 
	import zlib.httpFile;
	if( ..zlib.httpFile.download(url,"正在下载 SSR Core（ShadowsocksR-native），版本：" + versionTag
		,..io.fullpath("/download/ssr/")
		,..io.fullpath("/temp/ssr/"),,..mainForm.hwnd) ){
			..mainForm.disabled = false;
			
			if(..io.exist(..io.fullpath("/temp/ssr/ssr-client.exe"))){
				import sysProxy;
				sysProxy.reset(false);

				import process.file;
				process.file.terminate(..io.joinpath(coreDir,"ssr-client.exe")); 
				
				..io.createDir(coreDir); 
				fsys.copy(..io.fullpath("/temp/ssr/*.*"),coreDir); 
				
				// 更新完成后再写入新版本tag
				..config.core.ssrVersion = versionTag;
				..config.core.save();
				return versionTag,false;
			} 
			else {
				self.lastDownloadingCoreFailed = true;
			} 
	}
	else {
		..mainForm.disabled = false;
		self.lastDownloadingCoreFailed = true; 
	} 
}

/**intellisense(xray.core.ssr) 
restart(.(editor,outbound) = 重启启动 SSR 服务进程
lastDownloadingCoreFailed = 上次下载 Core 是否失败,如果希望重新下载请重新赋值为 null
end intellisense**/
