import win.ui;
import fonts.fontAwesome;
/*DSG{{*/
var frmXray = win.form(text="WinXray ";right=959;bottom=591;bgcolor=16777215)
frmXray.add(
btnEditPacOrUwp={cls="plus";text="编辑PAC";left=332;top=367;right=438;bottom=397;align="left";db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF1C4';notify=1;textPadding={left=30};z=7};
btnImportServerFromClipBd={cls="plus";text="批量导入链接";left=459;top=367;right=592;bottom=397;align="left";db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF196 ';notify=1;textPadding={left=30};z=8};
btnTcping={cls="plus";text="检测并连接最快服务器";left=735;top=366;right=941;bottom=398;align="left";bgcolor=11580047;border={radius=4};db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=18}};iconText='\uF012';notify=1;textPadding={left=42};z=3};
chkAutoTest={cls="plus";text="异常自动重连 ";left=599;top=367;right=730;bottom=397;align="left";db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF0C8';notify=1;textPadding={left=30};z=9};
edit={cls="edit";left=7;top=404;right=954;bottom=584;autohscroll=false;bgcolor=0;color=16777215;db=1;dl=1;dr=1;edge=1;multiline=1;vscroll=1;z=2};
listview={cls="listview";left=4;top=5;right=954;bottom=358;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;gridLines=1;z=1};
radioProxy={cls="radiobutton";text="全局代理";left=133;top=367;right=230;bottom=397;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);z=4};
radioProxyDirect={cls="radiobutton";text="不使用代理";left=18;top=367;right=124;bottom=397;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);z=6};
radioProxyPac={cls="radiobutton";text="PAC代理";left=239;top=367;right=326;bottom=397;bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-13);z=5}
)
/*}}*/

frmXray.listview.insertColumn("类型",100) 
frmXray.listview.insertColumn("服务器",160) 
frmXray.listview.insertColumn("端口",70) 
frmXray.listview.insertColumn("加密",90) 
frmXray.listview.insertColumn("协议",50) 
frmXray.listview.insertColumn("备注",190) 
frmXray.listview.insertColumn("响应速度",95) 
frmXray.listview.insertColumn("状态",-1);
// 减少闪烁，详见：https://learn.microsoft.com/zh-cn/windows/win32/controls/extended-list-view-styles
frmXray.listview.setExtended(0x10000/*_LVS_EX_DOUBLEBUFFER*/);

frmXray.show();

import config;
import xray.core; 
var currentOutboundIndex,currentOutboundAddress;
var activeOutbound = function(outboundIndex,outboundAddress,enableRetry,timerTesting){
	// 如果没有值，则退出
	if(!outboundIndex && !outboundAddress){
		outboundIndex,outboundAddress = currentOutboundIndex,currentOutboundAddress;
		if(!outboundIndex && !outboundAddress) {
			return;
		}
	}
	
	// 如果配置文件中没有值，那么就退出
	var outbound = config.proxy.outbounds[outboundIndex];
	if(!outbound){
		return ; 
	} 
 	
 	// 按 按钮之后，传进来的是空，所以会进入下面的判断
	var startTesting = timerTesting;
	if(!startTesting){
		..publish("activeOutbound",false); 
		
		var started,err = xray.core.restart(frmXray.edit,outbound);
		startTesting = started;
		
		if(!started){
			frmXray.edit.print("启动 Xray 错误",err:""); 
			frmXray.btnTcping.disabledText = null;
			return false;
		}
	}

	if( startTesting  ){
		currentOutboundIndex,currentOutboundAddress = outboundIndex,outboundAddress;
		frmXray.autoTesting = true;
		
		..thread.invoke( 
			function(frmXray,outboundIndex,outboundAddress,sockProxyPort,httpProxyAddress,enableRetry,timerTesting){
				import web.rest.client;
				var restClient = web.rest.client(,httpProxyAddress);
				restClient._http.setTimeouts(1000,2000,2000);
				var ret = restClient.get("https://www.google.com/generate_204");
				var lastStatusCode = restClient.lastStatusCode;
				
				if( lastStatusCode == 204 ){
					frmXray.onStartXrayComplete(outboundIndex,timerTesting,outboundAddress); 
					return;	
				}
				restClient.close();
				
				if(timerTesting){
					sleep(100);
				}

				
				import wsock.tcp.socks5Client;
				
				var client;
				for(i=1;2;1){
					sleep(200);
					client = wsock.tcp.socks5Client("127.0.0.1",sockProxyPort); 
					if(client)break;
				}
				
				if(client){
					client.setTimeouts(1000,1000); 
					if(client.connect("www.google.com",80) ){
						
						var sendData =/***********
GET /  HTTP/1.1
Host: www.google.com
Connection: close
User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64)
Accept: */*;  
Accept-Language: zh-CN,zh; 
Accept-Charset:utf-8;  
***********/
						client.write( sendData + '\r\n\r\n' )
						var rep = client.read(9);
						client.close();
							
						if( rep && ..string.startWith(rep,"HTTP/1.1 ") ){
							frmXray.onStartXrayComplete(outboundIndex,timerTesting,outboundAddress);
							return;	
						} 
					}
				};
				frmXray.onStartXrayFailed(outboundIndex,enableRetry,outboundAddress);
			},frmXray,outboundIndex,outboundAddress, 
			xray.core.socksProxyPort,xray.core.getSystemProxyConfig(),enableRetry,timerTesting
		)

		return true;
	}	
}

var autoUpdateSubscriptionTimerId; 
frmXray.autoUpdateSubscription = function(timeout){
		..win.clearTimeout(autoUpdateSubscriptionTimerId); 
		if(timeout===-1){
			autoUpdateSubscriptionTimerId = null;
			return;
		}
		
		if(config.proxy.autoRefreshSubscription){
			autoUpdateSubscriptionTimerId = ..win.setTimeout(function(){
				autoUpdateSubscriptionTimerId = null;
				
				import xray.outbounds;
				xray.outbounds.autoUpdateSubscription(); 
			},timeout:10000)  		
		}
} 

frmXray.onStartXrayComplete = function(outboundIndex,timerTesting,outboundAddress){
	// 停止自动更新
	frmXray.autoUpdateSubscription(-1);
	
	var outbound = config.proxy.outbounds[outboundIndex];
	var outbound = ..config.proxy.outbounds[outboundIndex];

	// 如果出站代理不存在，或者地址与给定地址不匹配，则退出函数
	if(!(outbound && (outbound.address == outboundAddress))){
		return;
	}
	
	var listview = frmXray.listview;
	listview.ensureVisible(outboundIndex);
	//清空所有出站代理的延迟测试结果
	for b,v in table.eachIndex(config.proxy.outbounds){
		listview.setItemText("",b,8);
	}
	
	// 停止自动测试，并更新 ListView 中出站代理的状态
	frmXray.autoTesting = false;
	listview.setItemText("活动服务器：正常",outboundIndex,8);
	listview.activeOutboundIndex = outboundIndex;

	// 获取出站代理地址
	var address  = listview.getItemText(outboundIndex,2);
	// 如果没有测试定时器，则打印出站代理地址
	if(!timerTesting) frmXray.edit.print("已切换到服务器：",address);
	listview.activeOutboundAddress = address;

	// 如果之前出站代理不可用，则清空其状态
	if(listview.getItemText(outboundIndex,7)=="不可用"){
		listview.setItemText("",outboundIndex,7);
	}
	
	// 重写回调函数，新的可用节点不再存储到frmXray.validOutbounds中
	frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){
		if(address==listview.getItemText(outboundIndex,2)){
			listview.setItemText(speedText,outboundIndex,7);
		}
	};
	// 清空有效出站代理的列表
	frmXray.validOutbounds = {};
	
	// 恢复 TCPing 按钮
	frmXray.btnTcping.disabledText = null;
	// 发布当前活动出站代理的地址
	..publish("activeOutbound",address); 
}

subscribe("activeOutbound",function(address){
	..globalActiveOutbound = address;
} )

frmXray.onStartXrayFailed = function(outboundIndex,enableRetry,outboundAddress){ 
	..publish("activeOutbound",false); 

	var outbound = ..config.proxy.outbounds[outboundIndex];
	if(!(outbound && (outbound.address == outboundAddress))){
		return;
	}
	
	if(outbound.subscribeUrl){
		frmXray.autoUpdateSubscription();
	} 
	
	var listview = frmXray.listview;
	listview.ensureVisible(outboundIndex);
	if(listview.activeOutboundIndex==outboundIndex){
		listview.activeOutboundIndex=null;
		listview.activeOutboundAddress = null;
	}
	
	listview.setItemText("不可用",outboundIndex,7);
	if(enableRetry){
		if(#frmXray.validOutbounds){
			var next = table.shift(frmXray.validOutbounds);
			activeOutbound(next[1],next[2],#frmXray.validOutbounds);
			listview.setItemText("异常,测试下一个...",outboundIndex,8);
			frmXray.edit.print("服务器异常：",listview.getItemText(outboundIndex,2)); 
			frmXray.edit.print("正在尝试连接：",next[2]); 	
		}
		else {
			frmXray.btnTcping.oncommand();
		}
	}
	else {
		listview.setItemText("活动服务器：异常",outboundIndex,8);
		frmXray.edit.print("已切换到服务器：",listview.getItemText(outboundIndex,2)); 
		frmXray.autoTesting = false;
	} 
	
	frmXray.btnTcping.disabledText = null;
	
}

// 测延迟
var tcping = function(frmXray,outboundIndex,outbound){ 
	import wsock.tcp.client; 
	var timeout = 0; 
	var failed = 0;
	// 测试8次，取平均值作为延迟
	for(i=1;8;1){ 
		sleep(1);
		
		var client = wsock.tcp.client(); 	
		var tickBegin = time.tick(); 
		
		if(client.connectTimeout(outbound.address,tonumber(outbound.port),3)){
			// 发送成功的话，返回“.”的长度（1）给ret
			var ret = client.send("."); 
			client.close();
			
			// 没有发送成功
			if( ret != 1){  
				frmXray.onTcpingReturn(outboundIndex,outbound.address,false,"不可用");
				return;
			}
			// 累计时间，后面求平均值
			timeout =  timeout + (time.tick() - tickBegin);
		}
		else {
			// 3秒超时的情况
			client.close();
			
			failed++;
			if(failed>1){ 
				frmXray.onTcpingReturn(outboundIndex,outbound.address,false,"不可用");
				return;	
			} 
		} 
	}
	
	// 调用的是frmXray.btnTcping.oncommand中的onTcpingReturn
	// 如果后面发现有调用其他地方的onTcpingReturn，则需要将该功能单独抽出来
	frmXray.onTcpingReturn(outboundIndex,outbound.address,true,math.round(timeout / (8-failed) / 1000,2)+"秒"); 
};

var retryOnNetworkAliveTimerId;
frmXray.onTcpingFailed = function(){
	frmXray.autoTesting = false;
	frmXray.btnTcping.disabledText = null;	
	..publish("activeOutbound",false); 
	
	// 如果有值，就清除定时器
	if(retryOnNetworkAliveTimerId){
		frmXray.clearTimeout(retryOnNetworkAliveTimerId);
	}
	
	// 不使用代理服务器时，有没有网络
	if(inet.http.isAlive(,false)){
		// 有网络时，自动更新订阅
		frmXray.autoUpdateSubscription(1000);
	}
	else {
		// 没有网时，创建一个定时器，每隔1秒执行一次匿名函数
		retryOnNetworkAliveTimerId = frmXray.setInterval( 
			function(){ 
				// 如果有激活了的服务器，那么retryOnNetworkAliveTimerId设为空，
				// 并返回0，表示不再执行定时器函数
				if(frmXray.listview.activeOutboundIndex){
					retryOnNetworkAliveTimerId = null;
					return 0;
				}
				
				if(inet.http.isAlive(,false)){
					// 检测到有网后，重新测延迟，并清除定时器
					frmXray.btnTcping.oncommand();
					retryOnNetworkAliveTimerId = null;
					return 0;
				}	
			},1000
		) 
	}  
} 

import sysProxy; 
import style;
frmXray.btnTcping.skin(style.primaryButton);
frmXray.btnTcping.oncommand = function(id,event){
	frmXray.listview.activeOutboundIndex = null;
	frmXray.listview.activeOutboundAddress = null;
	frmXray.listview.clearColumnImage();
	
	var items = frmXray.listview.items;
	if(#items){
		frmXray.btnTcping.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
		
		xray.core.lastDownloadingCoreFailed = false;
		// 如果没有找到 xray core，那么就退出函数
		if(!xray.core.getPath()){ 
			frmXray.btnTcping.disabledText = null; 
			return;
		} 

		var failedCount = 0;
		frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){
			if(address!=frmXray.listview.getItemText(outboundIndex,2)){
				return; 
			} 
			// 无论连接是否成功，都要显示延迟时间
			frmXray.listview.setItemText(speedText,outboundIndex,7);

			if(succeeded){
				// 接收到第一个成功返回的节点后，改写frmXray.onTcpingReturn，使后面的成功节点
				// 都只是放到frmXray.validOutbounds。之后激活第一个成功节点
				frmXray.validOutbounds = {}
				frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){
					if(address!=frmXray.listview.getItemText(outboundIndex,2)){
						return; 
					} 
					
					frmXray.listview.setItemText(speedText,outboundIndex,7);
					if(succeeded) { 
						// 后续成功节点都放到全局变量frmXray.validOutbounds中
						table.push(frmXray.validOutbounds,{outboundIndex;address});
					}
					else{
						failedCount++;
						if(failedCount==#items) {
							frmXray.onTcpingFailed();
						}
					}
				}
				// 激活第一个节点
				// #items>1 表示节点数大于1时，启动重试。可能需要改成 #validOutbounds>0
				activeOutbound(outboundIndex,address,#items>1);	
			}
			else{
				failedCount++;
				// 所有节点都不通时执行
				if(failedCount==#items) {
					frmXray.onTcpingFailed(); 
				}
			}
		}
			
		import thread.manage;
		var manage = thread.manage(config.proxy.maxTestServers);
		var outbounds = config.proxy.outbounds;
		var listview = frmXray.listview;
		
		// 测速前，初始化显示内容
		for(i=1;#items;1){
			// 默认全部允许测速，不允许的话，需要手动勾选
			if(outbounds[i].autoConnect===false){
				listview.setItemText("禁止测速",i,7);
				continue;
			}
	
			listview.setItemText("...",i,7);
			listview.setItemText("",i,8);
			
			// 创建测速线程
			manage.create(tcping,frmXray,i,config.proxy.outbounds[i]);
		}
		manage.waitClose(); 
	}
	else {
		if(id) frmXray.msgWarn("请先添加服务器")
		xray.core.stop();
		
		frmXray.btnTcping.disabledText = null;
		frmXray.autoTesting = false;
		frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){};
	}
}

import xray.core;
import xray.pacServer;
frmXray.onDestroy = function(){
	xray.core.stop();
	xray.pacServer.stop();
}

var pingThread = function( ip,frmXray ) {
	import icmp.ping; 
	var ping = icmp.ping(); 
	
	for(i=1;4;1){
		if( ping.perform(ip) ){
				frmXray.edit.print(ip,string.format( "Reply from %s: bytes=%d time=%dms TTL=%d"
				, ping.ip , #ping.requestData  , ping.echoReply.roundTripTime , ping.echoReply.options.ttl ) ); 
		}  
		else {
			frmXray.edit.print(ip,"Request timed out");
		}
		
	}
} 

var removeOutbounds = function(selectedItems){
	
	//避免后台线程改变activeIndex
	frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){}; 
	frmXray.btnTcping.disabledText = null;
	frmXray.autoTesting = false;
	
	var activeIndex = frmXray.listview.activeOutboundIndex;  
	var outbounds = config.proxy.outbounds;
	for(i=#outbounds;1;-1){ 
		var ob = outbounds[i];
		if(activeIndex==i){
			ob.active = true;
		}
	}

	if(#selectedItems){
		for(i=#selectedItems;1;-1){
			var item = selectedItems[i] 
			frmXray.listview.delItem(item);
			..table.remove(config.proxy.outbounds,item);
		}
	}
	else{
		for(i=#outbounds;1;-1){ 	
			if(frmXray.listview.getItemText(i,7)=="不可用"){
				..table.remove(outbounds,i);
				frmXray.listview.delItem(i);
			} 
		}
	}

	frmXray.listview.activeOutboundIndex = null;
	for(i=1;#outbounds;1){
		if(outbounds[i].active){
			frmXray.listview.activeOutboundIndex = i;
			break;
		}
	}
	publish("outbounds.updateConfigJson"); 

	if(activeIndex && (!frmXray.listview.activeOutboundIndex) ){
		if(!xray.core.lastDownloadingCoreFailed) frmXray.btnTcping.oncommand();
	}
}

import xray.outbounds;
frmXray.listview.onnotify = function(id,code,ptr){
	select(code) {
		case 0xFFFFFFF4/*_NM_CUSTOMDRAW*/ {
			var lvcd = frmXray.listview.getNotifyCustomDraw(code,ptr);
			if( lvcd.nmcd.dwDrawStage == 0x10001/*_CDDS_ITEMPREPAINT*/)
				return 0x20/*_CDRF_NOTIFYSUBITEMDRAW*/
			elseif( lvcd.nmcd.dwDrawStage == 1/*_CDDS_PREPAINT*/ ){
				return 0x20/*_CDRF_NOTIFYITEMDRAW*/;
			}
			elseif( lvcd.nmcd.dwDrawStage == ( 0x10001/*_CDDS_ITEMPREPAINT*/ | 0x20000/*_CDDS_SUBITEM*/) ){ 
				//注意这里 iSubItem 的索引自0开始( 其他函数通常自1开始 )
				lvcd.clrText = lvcd.nmcd.dwItemSpec % 2 ? 0x373737 : 0;
				lvcd.clrTextBk = lvcd.nmcd.dwItemSpec % 2 ? 0xFFFFFF : 0xF5F5F5;
				
				var item = lvcd.nmcd.dwItemSpec+1;
				if(lvcd.iSubItem==7){ 
					if(frmXray.listview.activeOutboundIndex=item){ 
						lvcd.clrTextBk = 0x8AFFC3
					} 
				}
				elseif(lvcd.iSubItem==6){ 
					if(..config.proxy.outbounds[item].autoConnect===false){ 
						lvcd.clrText = 0xCCCCCC;
					} 
				}
				lvcd.update()
				
				return 0/*_CDRF_DODEFAULT*/
			}
		}
		case 0xFFFFFFFD/*_NM_DBLCLK*/ {
			var nm = frmXray.listview.getNotifyMessage(code,ptr);
			if( nm ){
				xray.core.lastDownloadingCoreFailed = false;
				
				frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){
					if(address==frmXray.listview.getItemText(outboundIndex,2)){
						frmXray.listview.setItemText(speedText,outboundIndex,7);
					} 
				}
				frmXray.btnTcping.disabledText = null;
				
				for b,v in table.eachIndex(config.proxy.outbounds){
					frmXray.listview.setItemText("",b,8);
				}
				activeOutbound(nm.iItem,frmXray.listview.getItemText(nm.iItem,2),false); 
			}
		}
		case 0xFFFFFFFB/*_NM_RCLICK*/  {
			var x,y = win.getCursorPos();
			var nm = frmXray.listview.getNotifyMessage(code,ptr);
			if(!nm) return;

			var currentIdx = nm.iItem;
			if(!currentIdx){
				var popmenu = win.ui.popmenu(frmXray);
				popmenu.add("新增代理服务器",function(id){ 
					var frmOutbound = frmXray.loadForm("\forms\main\outbound.aardio"); 
					frmOutbound.doModal() 
				});
				
				popmenu.add("自剪贴板批量导入分享链接、订阅源",function(id){
					frmXray.btnImportServerFromClipBd.oncommand();
				});
				
				popmenu.add('立即更新所有订阅源',function(id){
					xray.outbounds.updateSubscription();
				});
				
				popmenu.add(); 
				
				popmenu.add('清空服务器列表',function(id){
					config.proxy.outbounds = ..table.array();
					frmXray.listview.clear();
					
					frmXray.btnTcping.disabledText = null;
					frmXray.autoTesting = false;
					frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){}  
					publish("outbounds.updateConfigJson");
				});
				
				popmenu.add('清除「不可用」节点',function(id){
					removeOutbounds();
				});	
				
				popmenu.add('清除「重复」节点',function(id){
					var removecount = frmXray.removeRepeat();
					if(removecount){
						frmXray.edit.print("删除了"+removecount+"个节点")
						}
				});
				
				popmenu.add();
				
				if(!frmXray.btnTcping.disabledText){
					popmenu.add('按「响应速度」排序服务器',function(id){
						frmXray.listview.sortBySpeed();
					});	 
					popmenu.add();
				}; 
				popmenu.popup(x,y,true);
				return;
			}
			
			var selectedItems = frmXray.listview.selected; 
			var popmenu = win.ui.popmenu(frmXray);
			
			if(#selectedItems==1){
				popmenu.add('设为活动服务器( 鼠标双击 )',function(id){
					if(currentIdx){
						xray.core.lastDownloadingCoreFailed = false;
						
						frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){
							if(address==frmXray.listview.getItemText(outboundIndex,2)){
								frmXray.listview.setItemText(speedText,outboundIndex,7);
							} 
						}
						
						frmXray.btnTcping.disabledText = null;
						
						for b,v in table.eachIndex(config.proxy.outbounds){
							frmXray.listview.setItemText("",b,8);
						}
						activeOutbound(currentIdx,frmXray.listview.getItemText(currentIdx,2),false)
					}
				});

				popmenu.add();
			}
			 
			popmenu.add("编辑 / 新增代理服务器",function(id){ 
				var frmOutbound = frmXray.loadForm("\forms\main\outbound.aardio");
				frmOutbound.setOutboundConfig(config.proxy.outbounds[currentIdx]);
				frmOutbound.doModal() 
			});
			
			popmenu.add("自剪贴板批量导入分享链接、订阅源",function(id){
				frmXray.btnImportServerFromClipBd.oncommand();
			});
			 
			var subscribeUrl = config.proxy.outbounds[currentIdx][["subscribeUrl"]];
			if(#selectedItems=1 && subscribeUrl){
				
				var ps;
				var cfgSubscribeUrls = ..config.proxy.subscribeUrls;
				for(i=#cfgSubscribeUrls;1;-1){
					var sub = cfgSubscribeUrls[i]  
					if(sub.url===subscribeUrl) {
						ps = sub.ps;
						break;
					}
				}
				
				if(#ps){
					ps = ": "+ps;
				}
			
				popmenu.add('更新当前订阅源'+ps,function(id){
					if(currentIdx){
						xray.outbounds.updateSubscription({[subscribeUrl]=true});
					}
				});	
			}

			popmenu.add('更新所有订阅源',function(id){
				if(currentIdx){
					xray.outbounds.updateSubscription();
				}
			});	
			 
			popmenu.add()

			var autoConnect = config.proxy.outbounds[currentIdx].autoConnect!==false; 
			var id = popmenu.add('允许自动测速并连接',function(id){
				if(currentIdx){
					for(i=#selectedItems;1;-1){ 
						var item = selectedItems[i];
						if(autoConnect === false){
							config.proxy.outbounds[item].autoConnect = null;
							frmXray.listview.setItemText("",item,7);
						}
						else {
							config.proxy.outbounds[item].autoConnect = false;
							frmXray.listview.setItemText("禁止测速",item,7);
						}
					}
				}
			});
			popmenu.check(id,autoConnect,0/*_MF_BYCOMMAND*/); 
			popmenu.add()
			
			popmenu.add('删除服务器',function(id){
				removeOutbounds(selectedItems);
			});
				
			popmenu.add('清空服务器列表',function(id){
				if(currentIdx){
					config.proxy.outbounds = ..table.array();
					frmXray.listview.clear();
					publish("outbounds.updateConfigJson");
				}
			});
			
			popmenu.add('清除「不可用」节点',function(id){
				removeOutbounds();
			});	
			
			popmenu.add('清除「重复」节点',function(id){
				var removecount = frmXray.removeRepeat();
				if(removecount){
					frmXray.edit.print("删除了"+removecount+"个节点")
					}
				});
				
			 
			if(#selectedItems==1){	
		 
				popmenu.add();
				popmenu.add('生成二维码',function(id){
					if(currentIdx){
						import xray.outbounds;
						import win.clip;
						
						var str = xray.outbounds.exportSharedLinks({config.proxy.outbounds[currentIdx]});
						if(str){ 
							var frmChild = frmXray.loadForm("\forms\main\tools\qr.aardio");
							frmChild.createQrCode(str);
							frmChild.show();
						}  
					}
				});
				
				popmenu.add('复制服务器分享链接',function(id){
					if(currentIdx){
						import xray.outbounds;
						import win.clip;
						
						var str = xray.outbounds.exportSharedLinks({config.proxy.outbounds[currentIdx]});
						if(str){ 
							win.clip.write(str); 
							frmXray.edit.print("已复制链接:",str);
						}  
					}
				});

				popmenu.add();
				
				var toolMenu = win.ui.popmenu(frmXray)
				toolMenu.add('本机 Ping 检测',function(id){
					if(currentIdx){
						thread.invoke(pingThread,config.proxy.outbounds[currentIdx].address,frmXray)
					}
				});
					
				toolMenu.add('全球范围 Ping 检测 ...',function(id){
					if(currentIdx){
						process.openUrl("http://ping.pe/" + config.proxy.outbounds[currentIdx].address)
					}
				});
				
				toolMenu.add('全球范围端口检测 ...',function(id){
					if(currentIdx){
						process.openUrl("http://port.ping.pe/" + config.proxy.outbounds[currentIdx].address + ":" + config.proxy.outbounds[currentIdx].port)
					}
				});
				
				toolMenu.add('在线路由跟踪检测 ...',function(id){
					if(currentIdx){
						process.openUrl("https://www.boce.com/traceroute/" + config.proxy.outbounds[currentIdx].address)
					}
				});
				
				toolMenu.add('本机路由检测',function(id){
					if(currentIdx){
						import process;
						process.execute("cmd.exe","/k tracert " + config.proxy.outbounds[currentIdx].address);
					}
				});

				if(config.proxy.outbounds[currentIdx].port == 443 && config.proxy.outbounds[currentIdx].tls){
					toolMenu.add('浏览 HTTPS 主页 ...',function(id){
						if(currentIdx){
							process.openUrl("https" ++ "://" + config.proxy.outbounds[currentIdx].address); 
						}
					});	 
				}
				
				popmenu.add("服务器工具",toolMenu)
				
				popmenu.add('SSH 登录服务器',function(id){
					if(currentIdx){
						if(_STUDIO_INVOKED){
							frmXray.msgErr("请在发布后运行此功能");
							return;
						}
						import process;
						process.execute(io._exepath,"/sshLogin root@" 
							+ config.proxy.outbounds[currentIdx].address + ":" 
							+ (config.proxy.outbounds[currentIdx].sshPort||22));
					}
				});  
			}
			if(#selectedItems>1){
				popmenu.add();
				popmenu.add('批量生成二维码',function(id){
					if(currentIdx){
						var outbounds = ..table.array();
						for(i=1;#selectedItems;1){
							..table.push(outbounds,config.proxy.outbounds[selectedItems[i]]);
						}
						
						var str = xray.outbounds.exportSharedLinks(outbounds);
						if(str){
							var frmChild = frmXray.loadForm("\forms\main\tools\qr.aardio");
							frmChild.createQrCode(str);
							frmChild.show();
						}  
					}
				});
				
				popmenu.add('批量复制服务器分享链接',function(id){
					if(currentIdx){
						var outbounds = ..table.array();
						for(i=1;#selectedItems;1){
							..table.push(outbounds,config.proxy.outbounds[selectedItems[i]]);
						}
						
						var str = xray.outbounds.exportSharedLinks(outbounds);
						if(str){
							import win.clip;
							win.clip.write(str);
							frmXray.msgOk("已将选中链接的分享链接写入剪贴板",1200);
						}	
					}
				});

				popmenu.add();
			}
			
			popmenu.popup(x,y,true);
		}
		case 0xFFFFFF94/*_LVN_COLUMNCLICK*/{
			var nm = frmXray.listview.getNotifyMessage(code,ptr)
			if(nm.iSubItem==7){
				frmXray.listview.sortBySpeed();
			} 
			else {
				frmXray.msgWarn("排序请点击「响应速度」列")
			}
		}
	}
}

// 排序函数
frmXray.listview.sortBySpeed = function () {
	if(frmXray.btnTcping.disabledText){
		frmXray.msgWarn("请等待测速完成，然后再点击此排序列。",1000);
		return;
	}
	
	var outbounds = ..table.clone(config.proxy.outbounds);
	var activeIndex = frmXray.listview.activeOutboundIndex;
	frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){}; //避免后台线程改变activeIndex
	
	for(i=#outbounds;1;-1){
		var ob = outbounds[i];
		if(type(ob)!="table"){
			table.remove(outbounds,i);
			continue;
		}
		
		ob.speed = frmXray.listview.getItemText(i,7);
		if(ob.speed=="不可用"){
			ob.speedNum = 0xFFFE;
		}
		elseif(ob.speed==""){
			ob.speedNum = 0xFFFF;
		}
		elseif(ob.speed=="..."){
			frmXray.msgWarn("请等待检测速度完成，然后再点击此排序列。",1000)
			return;
		}
		else {
			ob.speedNum = tonumber(ob.speed):-3;
		} 
		
		if(activeIndex==i){
			ob.active = true;
		}
	}
	
	var hasStatus;
	for(i=1;#outbounds;1){
		if(#outbounds[i].speed){
			hasStatus = true;
			break;
		}
	}
	
	if(!hasStatus){
		frmXray.msgWarn("请先点击「检测并连接服务器」获取服务器响应速度！");
		return;
	}
	
	var desc = frmXray.listview.getColumnImage(7) == 0;
	if(desc){
		table.sort(outbounds,function(b){ 
			return owner.speedNum > b.speedNum;
		})	
	}
		else {
			table.sort(outbounds,function(b){ 
			return owner.speedNum < b.speedNum;
		})
		} 
	
	frmXray.listview.activeOutboundIndex = null;
	for(i=1;#outbounds;1){
		if(outbounds[i].active){
			frmXray.listview.activeOutboundIndex = i;
			break;
		}
	}
	
	outbounds.fields = {"protocol";"address";"port";"security";"network";"ps";"speed"}
	config.proxy.outbounds = outbounds;
	frmXray.listview.setTable(outbounds);	
	outbounds.fields = {"protocol";"address";"port";"security";"network";"ps"}
	
	//清理用于排序的临时字段
	for(i=1;#outbounds;1){
		var ob = outbounds[i];
		ob.speedNum = null;
		ob.speed = null;
		ob.active = null;
	} 
	
	if(frmXray.listview.activeOutboundIndex){
		frmXray.listview.setItemText("活动服务器：正常",frmXray.listview.activeOutboundIndex,8)
	} 
	frmXray.listview.setColumnImage(7, desc ? 1 : 0);
}
//移除重复节点(保留序号在前的)
frmXray.removeRepeat=function(){
	//跳过检测异常
	frmXray.listChanging = true;
	//避免后台线程改变activeIndex
	frmXray.onTcpingReturn = function(outboundIndex,address,succeeded,speedText){}; 
	frmXray.btnTcping.disabledText = null;
	frmXray.autoTesting = false;
	//标记活动节点
	var activeIndex,activeText = frmXray.listview.activeOutboundIndex;  
	var outbounds = ..table.clone(config.proxy.outbounds);
	if( activeIndex ){
		outbounds[activeIndex].active = true;
		activeText = frmXray.listview.getItemText(activeIndex);
	}
	//标记重复节点
	for(i=1;#outbounds;1){
		for(j=1;#outbounds;1){
			if( j<=i ){ continue }
			if( outbounds[j].address==outbounds[i].address 
				&& outbounds[j].port==outbounds[i].port ){
				outbounds[j].repeat=true;
			}
		}
	}
	//删除重复节点
	var removecount=0;
	for(i=#outbounds;1;-1){
		if(outbounds[i].repeat){
			table.remove(outbounds,i);
			removecount++;
		}
	}
	//重设活动节点序号
	frmXray.listview.activeOutboundIndex = null;
	for(i=1;#outbounds;1){
		var ob = outbounds[i];
		if(ob.active){
			frmXray.listview.activeOutboundIndex = i;
			activeIndex = i;
		}
		ob.repeat = null;
		ob.active = null;
	}
	//保存配置文件
	config.proxy.outbounds = outbounds;
	config.proxy.save();
	frmXray.listview.setTable(outbounds);
	//重新测速
	frmXray.btnTcping.oncommand();
	//继续检测异常
	frmXray.listChanging = false;
	
	return removecount; 
}
//按键
import win.ui.accelerator;
var accelerator = win.ui.accelerator({
	{//按下Enter键
		vkey = '13';
		oncommand=function(){
			frmXray.btnTcping.oncommand();			
		}	   
	};
	{ //按下DELETE键
		vkey = '46'; 
		oncommand = function(){
			var selectedItems = frmXray.listview.selected;
			removeOutbounds(selectedItems);
		}
	};
	{ //按下ctrl+C
		ctrl = true; vkey = 'C'#; 
		oncommand = function(){
			var selectedItems = frmXray.listview.selected;
			if( !#selectedItems ){
				frmXray.msgFrown("没有选中节点！");
				return ; 
			}
			
			var outbounds = ..table.array();
			for(i=1;#selectedItems;1){..table.push(outbounds,config.proxy.outbounds[selectedItems[i]])}
			var str = xray.outbounds.exportSharedLinks(outbounds);
			if(str){
				import win.clip;
				win.clip.write(str);
				frmXray.msgOk("已将选中节点的分享链接写入剪贴板",1200);
			}
		}
	};
	{//ctrl+V
		ctrl = true; vkey = 'V'#;
		oncommand=function(){
			frmXray.btnImportServerFromClipBd.oncommand();
		}
	};
	{//ctrl+A
		ctrl = true; vkey = 'A'#;
		oncommand=function(){
			var outbounds=config.proxy.outbounds;
			for(i=#outbounds;1;-1){
				frmXray.listview.setSelected(i);
			}
		}
	};
},frmXray);
//文件拖放
frmXray.onDropFiles = function(files){
	for(i=1;#files;1){
		var bmp = gdip.bitmap(files[i]);
		if(bmp){
			import zbar;
			var scanner = zbar.scanner();
			scanner.config('qrcode.enable');
			scanner.scanBitmap(bmp,function(typeName,data){  
		    	import win.clip;
				win.clip.write(data);
				
				var outbounds = xray.outbounds.importFromString(data);
					if(#outbounds){
						..table.append(config.proxy.outbounds,outbounds); 
						publish("uiCommand.restartxrayCore");
						publish("outbounds.updateConfigJson");
						frmXray.msgOk("已成功导入" + #outbounds + "个服务器",1200); 
						return; 
			           }
			    })					
	 } 
	 
		var str=string.load(files[i]);
		if(str){
			str = ..string.trim(str,'"\'\t\r\n '); 
	
			var outbounds = xray.outbounds.importFromString(str);
					if(#outbounds){
						..table.append(config.proxy.outbounds,outbounds); 
						publish("uiCommand.restartxrayCore");
						publish("outbounds.updateConfigJson");
						frmXray.msgOk("已成功导入" + #outbounds + "个服务器",1200); 
						return; 
					}
		}
	}
	//frmXray.msgFrown('未能在文件中导入节点!');
}

import win.imageList;
var iml = win.imageList(16, 15);
iml.add('GIF\56\57a \0\15\0\x80\0\0\x80\x80\x80\xff\0\xff\33\xf9\4\0\0\0\0\0\44\0\0\0\0 \0\15\0\0\2\31\x8c\x8f\xa9\xcb\xed\15\xa3\x9c\xb4N\xf0\x80\xde\56k\xbfA\\\xd7\x84 \x97Y\xea\xca\xb6\xee\11\xc7F\1\0;', 0xff00ff);
frmXray.listview.setColumnImageList(iml);

// 读取配置，添加到列表，并测速、连接
subscribe("uiCommand.restartxrayCore",function(){ 
	config.proxy.outbounds.fields = {"protocol";"address";"port";"security";"network";"ps";}
	//frmXray.listview.fillParent();
	var outbounds = config.proxy.outbounds;
	for(i=#outbounds;1;-1){
		if(type(outbounds[i])!="table"){
			table.remove(outbounds,i);
		}
	}
	frmXray.listview.setTable(outbounds); 
	frmXray.listview.fillParent();
	// 程序首次执行时，lastDownloadingCoreFailed没有定义，所以下面一定会执行
	if(!xray.core.lastDownloadingCoreFailed) frmXray.btnTcping.oncommand();
} )

import process;
// 程序真正入口
// 如果有代理节点
if(#config.proxy.outbounds){
	frmXray.setTimeout( 
		function(){
			publish("uiCommand.restartxrayCore");	
		},1000
	);	
}

subscribe("sysProxy.modeChanged",function(mode){
	frmXray.radioProxyPac.checked = false;
	frmXray.radioProxy.checked = false;
	frmXray.radioProxyDirect.checked = false;
	
	if(mode = "pac"){
		frmXray.radioProxyPac.checked = true;
		if(_WIN10_LATER) frmXray.btnEditPacOrUwp.text = "编辑 PAC";
	}
	elseif(mode = "proxy"){
		frmXray.radioProxy.checked = true;
		if(_WIN10_LATER) frmXray.btnEditPacOrUwp.text = "配置 UWP";
	}
	else{
		frmXray.radioProxyDirect.checked = true;
	}
} )
// 设置代理
publish("sysProxy.modeChanged",config.proxy.mode);

import sysProxy;
var switchProxyMode = function(){
	if(frmXray.radioProxyPac.checked){
		sysProxy.switchToPacMode();	
	}
	elseif(frmXray.radioProxy.checked){
		sysProxy.switchToGlobalMode();	
	}
	else {
		sysProxy.switchToDirectMode();	
	}  
}
frmXray.radioProxyPac.oncommand = switchProxyMode;
frmXray.radioProxy.oncommand = switchProxyMode;
frmXray.radioProxyDirect.oncommand = switchProxyMode;

import win.dlg.message; 
frmXray.btnEditPacOrUwp.skin(style.plainButton)
frmXray.btnEditPacOrUwp.oncommand = function(id,event){
    if(frmXray.radioProxy.checked && _WIN10_LATER ){
        if(_STUDIO_INVOKED){
        	frmXray.msgErr("请先发布为 EXE！");
        	return;
        }
        
        import process;
    	process.execute(io._exepath,"/uwp","runas")
    	return;
    }
    
	if(frmPac && win.isWindow(frmPac.hwnd)){
		if( win.isIconic(frmPac.hwnd) ) win.show(frmPac.hwnd,9/*_SW_RESTORE*/ );
		if( !win.isVisible(frmPac.hwnd) ) win.show(frmPac.hwnd,0x1/*_SW_NORMAL*/ );
		win.setForeground(frmPac.hwnd)
		return;
	}

	frmPac = ..mainForm.loadForm("\forms\main\pac.aardio");
	frmPac.show();
}
subscribe("uiCommand.showPacForm",function(...){
	frmXray.btnEditPacOrUwp.oncommand();
} )

subscribe("uiCommand.print",function(...){
	frmXray.edit.print(...);
} )

import xray.outbounds;
frmXray.btnImportServerFromClipBd.skin(style.plainButton)
frmXray.btnImportServerFromClipBd.oncommand = function(id,event){
	var str = ..win.clip.read();
	if(str){  
		str = ..string.trim(str,'"\'\t\r\n '); 
		if( ..string.startWith(str,"http://") 
			|| ..string.startWith(str,"https://") 
			|| ..string.match(str,"^\s*/\N+\s*$") ){
			if(..string.match(str,"^\s*/\N+\s*$")){
				str = "https://github.com" + str;
			}
			
			xray.outbounds.updateSubscription({[str]=true});
			return;
		}

		var outbounds = xray.outbounds.importFromString(str);
		if(#outbounds){
			..table.append(config.proxy.outbounds,outbounds);
			// 防止首次粘贴时，下载core失败导致没有保存节点的情况，所以手动save一下
			config.proxy.save();
			frmXray.msgOk("已成功导入" + #outbounds + "个服务器",1200); 
			publish("uiCommand.restartxrayCore");
			publish("outbounds.updateConfigJson");
			return; 
		}
	}

	frmXray.msgFrown('未导入服务器！\r\n请先复制以下格式文本（自动清除其中的无效内容）：\r\n\r\n1、一行或多行（忽略无效行）分享链接或服务器JSON配置。\r\n支持 vmess://，vless://，ss://，ssr://，trojan://， trojan-go:// 等通用分享链接。\r\n\r\n2、包含多个服务器配置的JSON数组,支持WinXray格式以及通用格式JSON。\r\n\r\n3、单个 http:// 或 https:// 开头的通用订阅源地址。\r\n可直接使用浏览器地址栏的github文件地址（含blob或raw目录名）。\r\n也可以仅复制单斜杆开始的github文件路径。\r\n\r\n订阅源可用BASE64编码或明文返回以上1、2条规定的配置或分享链接。')
}

var testTimerId;
frmXray.chkAutoTest.oncommand = function(id,event){
	if(testTimerId){
		frmXray.clearInterval(testTimerId);
	}

	config.proxy.test = frmXray.chkAutoTest.checked;
	if(!config.proxy.test) return;

	import inet.http;
	testTimerId = frmXray.setInterval( 
		config.proxy.testInterval*1000,function(){
			if(frmXray.btnTcping.disabled) return;
			if( frmXray.autoTesting ) return; 

			var idx = frmXray.listview.activeOutboundIndex;
			var address = frmXray.listview.activeOutboundAddress;
			if( !( idx&&address) ){ 
				if(!inet.http.isAlive(,false)){
					return config.proxy.testInterval*1000;
				}
				
				if(!xray.core.lastDownloadingCoreFailed) frmXray.btnTcping.oncommand();
				return; 
			}
			
			frmXray.autoTesting = true; 
			activeOutbound(idx,address,true,true);
			
			return config.proxy.testInterval*1000;
		} 
	)
}

if(config.proxy.test){
	frmXray.chkAutoTest.checked = true;
	frmXray.chkAutoTest.oncommand();
}

frmXray.chkAutoTest.skin(style.checkBox);

frmXray.edit.enablePopMenu({
	{ /*---分隔线---*/ };
	{ "清屏";
		function(){
			var text = frmXray.edit.text;
			if(#text){
				frmXray.edit.text='';
			}
		}
	}; 
})

frmXray.enableDpiScaling();
win.loopMessage();

/**intellisense
tcping( = 代理节点延迟测试
tcping(.(编辑器 ,代理节点索引 ,代理节点) = 
end intellisense**/